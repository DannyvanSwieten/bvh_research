#version 460
#extension GL_EXT_nonuniform_qualifier: require
#extension GL_EXT_scalar_block_layout: require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_EXT_buffer_reference2 : require

struct Node{
    float min_x;
    float min_y;
    float min_z;
    float max_x;
    float max_y;
    float max_z;
    uint first_primitive;
    uint primitive_count;
};

struct Ray{
    vec3 origin;
    vec3 direction;
    vec3 invDirection;
};

struct Instance{
    uint64_t blas_address;
    uint64_t instance_id;
    mat4 transform;
};

layout(buffer_reference, scalar) readonly buffer BlasRef{
    Node nodes[];
};

layout(scalar, set = 0, binding = 0) readonly buffer TopLevelBvh{
    Node tlas[];
};

layout(scalar, buffer_reference_align = 4, set = 0, binding = 1) readonly buffer InstanceBuffer {
    Instance instances[];
};

layout(std430, set = 0, binding = 2) readonly buffer VertexBuffer{
    vec3 vertices[];
};

layout(std430, set = 0, binding = 3) readonly buffer IndexBuffer{
    ivec3 indices[];
};

layout(set = 0, binding = 4, rgba8) uniform image2D output_buffer;

#define FLOAT_MAX 100000

void swap_int(inout uint a, inout uint b){
    uint tmp = a;
    a = b;
    b = tmp;
}

void swap_float(inout float a, inout float b){
    float tmp = a;
    a = b;
    b = tmp;
}

float max_element(vec3 v){
    return max(v.x, max(v.y, v.z));
}

float min_element(vec3 v){
    return min(v.x, min(v.y, v.z));
}

float intersect_aabb(vec3 aabb_min, vec3 aabb_max, Ray ray, float t_far) {

    vec3 t_min = (aabb_min - ray.origin) * ray.invDirection;
    vec3 t_max = (aabb_max - ray.origin) * ray.invDirection;

    vec3 t1 = min(t_min, t_max);
    vec3 t2 = max(t_min, t_max);

    float t_near = max_element(t1);
    float t_f = min_element(t2);

    bool h = t_f > t_near && t_near < t_far && t_f > 0.0;
    if(h)
        return min(t_f, t_near);
    else
        return t_far;
}

float intersect_triangle(Ray ray, vec3 v0, vec3 v1, vec3 v2, inout float u, inout float v)
{
    vec3 edge1 = v1 - v0;
    vec3 edge2 = v2 - v0;
    vec3 h = cross(ray.direction, edge2);
    float a = dot(edge1, h);
    // Assume faceculling is enabled.
    if (a < 0.0001) {
        // Ray is parallel to the triangle
        return FLOAT_MAX;
    }

    float f = 1.0 / a;
    vec3 s = ray.origin - v0;
    u = f * dot(s, h);
    if (u < 0.0 || u > 1.0) {
        return FLOAT_MAX;
    }
    vec3 q = cross(s, edge1);
    v = f * dot(ray.direction, q);
    if (v < 0.0 || u + v > 1.0) {
        return FLOAT_MAX;
    }

    float t = f * dot(edge2, q);
    if (t < 0.000001) {
        return FLOAT_MAX;
    }

    return t;
}

float traverse_bttm_level(Ray ray, uint primitive_id, inout float u, inout float v);
float traverse_top_level(Ray ray, inout float u, inout float v)
{
    uint node_idx = 0;
    uint stack_ptr = 0;
    uint stack[64];
    float d = FLOAT_MAX;
    while(true) {
        Node node = tlas[node_idx];
        if (tlas[node_idx].primitive_count > 0) {
            uint first = node.first_primitive;
            uint last = first + node.primitive_count;
            for (uint p = first; p < last; ++p) {
                float lu, lv;
                float dl = traverse_bttm_level(ray, p, lu, lv);

                if (dl < d) {
                    d = dl;
                    u = lu;
                    v = lv;
                }
            }
            if (stack_ptr == 0) {
                break;
            } else {
                stack_ptr -= 1;
                node_idx = stack[stack_ptr];
                continue;
            }
        }

        uint left_child_idx = node.first_primitive ;
        uint right_child_idx = left_child_idx + 1;
        Node left_child = tlas[left_child_idx];
        Node right_child = tlas[right_child_idx];
        float left_distance = intersect_aabb(vec3(left_child.min_x, left_child.min_y, left_child.min_z), vec3(left_child.max_x, left_child.max_y, left_child.max_z), ray, FLOAT_MAX);
        float right_distance = intersect_aabb( vec3(right_child.min_x, right_child.min_y, right_child.min_z), vec3(right_child.max_x, right_child.max_y, right_child.max_z), ray, FLOAT_MAX);
        if (left_distance > d || right_distance > d){
            if(stack_ptr == 0){
                break;
            } else {
                stack_ptr -= 1;
                node_idx = stack[stack_ptr];
                continue;
            }
        }
        if (left_distance > right_distance) {
            swap_int(left_child_idx, right_child_idx);
            swap_float(left_distance, right_distance); 
        }
        if (left_distance == FLOAT_MAX) {
            if (stack_ptr == 0) {
                break;
            } else {
                stack_ptr -= 1;
                node_idx = stack[stack_ptr];
            }
        } else {
            node_idx = left_child_idx;
            if (right_distance != FLOAT_MAX) {
                stack[stack_ptr] = right_child_idx;
                stack_ptr += 1;
            }
        }
    }

    return d;
}

float traverse_bttm_level(Ray ray, uint instance_id, inout float u, inout float v)
{
    Instance instance = instances[instance_id];
    BlasRef blas_ref = BlasRef(instance.blas_address);
    uint node_idx = 0;
    uint stack_ptr = 0;
    uint stack[64];
    float d = FLOAT_MAX;
    mat4 inverse_transform = inverse(instance.transform);
    Ray rayInv;
    rayInv.origin = (inverse_transform * vec4(ray.origin, 1)).xyz;
    rayInv.direction = (inverse_transform * vec4(ray.direction, 0)).xyz;
    rayInv.invDirection = 1.0 / rayInv.direction;
    
    while(true) {
        Node node = blas_ref.nodes[node_idx];
        if (node.primitive_count > 0) {
            uint first = node.first_primitive;
            uint last = first + node.primitive_count;
            for (uint p = first; p < last; ++p) {
                ivec3 triangle = indices[p];
                float lu, lv;
                float dl = intersect_triangle(
                    rayInv,
                    vertices[triangle.x ].xyz,
                    vertices[triangle.y ].xyz,
                    vertices[triangle.z ].xyz,
                    lu, lv
                );

                if (dl < d) {
                    d = dl;
                    u = lu;
                    v = lv;
                }
            }
            if (stack_ptr == 0) {
                break;
            } else {
                stack_ptr -= 1;
                node_idx = stack[stack_ptr];
                continue;
            }
        }

        uint left_child_idx = node.first_primitive ;
        uint right_child_idx = left_child_idx + 1;
        Node left_child = blas_ref.nodes[left_child_idx];
        Node right_child = blas_ref.nodes[right_child_idx];
        float left_distance = intersect_aabb(vec3(left_child.min_x, left_child.min_y, left_child.min_z), vec3(left_child.max_x, left_child.max_y, left_child.max_z), rayInv, FLOAT_MAX);
        float right_distance = intersect_aabb( vec3(right_child.min_x, right_child.min_y, right_child.min_z), vec3(right_child.max_x, right_child.max_y, right_child.max_z), rayInv, FLOAT_MAX);

        if (left_distance > right_distance) {
            swap_int(left_child_idx, right_child_idx);
            swap_float(left_distance, right_distance); 
        }
        if (left_distance == FLOAT_MAX) {
            if (stack_ptr == 0) {
                break;
            } else {
                stack_ptr -= 1;
                node_idx = stack[stack_ptr];
            }
        } else {
            node_idx = left_child_idx;
            if (right_distance != FLOAT_MAX) {
                stack[stack_ptr] = right_child_idx;
                stack_ptr += 1;
            }
        }
    }

    return d;
}

Ray create_ray(vec2 resolution, vec2 frag_location, vec3 origin, float z){
    vec2 norm = frag_location / resolution;
    vec3 p0 = vec3(-1, 1, z);
    vec3 p1 = vec3(1, 1, z);
    vec3 p2 = vec3(-1, -1, z);

    vec3 pixel_position = 
            p0
            + (p1 - p0) * norm.x
            + (p2 - p0) * norm.y;

    vec3 direction = normalize(pixel_position - origin);
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    ray.invDirection = 1.0 / direction;
    return ray;
}

void main(){
    Ray ray = create_ray(vec2(640, 640), vec2(gl_GlobalInvocationID.xy), vec3(-5, 0, -15), 2);
    float u, v;
    float d = traverse_top_level(ray, u, v);
    if(d < FLOAT_MAX)
        imageStore(output_buffer, ivec2(gl_GlobalInvocationID.xy), vec4(1 - u - v, u, v, 1));
}